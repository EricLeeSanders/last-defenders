buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://repo.spring.io/plugins-release/" }
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.4'
        classpath 'com.google.firebase:perf-plugin:1.4.2'
        classpath 'com.google.gms:google-services:4.4.2'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:3.0.3'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:2.1.0'
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    version = '1.12.11'
    ext {
        appName = "Last Defenders"
        gdxVersion = '1.10.0'
        roboVMVersion = '2.3.12'
        gdxControllersVersion = '2.1.0'
        gdxPayVersion = '1.3.8'
        isDebug = "false"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
}

project(":desktop") {
    apply plugin: "java"

    dependencies {
        implementation project(":core")
        implementation "com.badlogicgames.gdxpay:gdx-pay-client:$gdxPayVersion"
        implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
        implementation "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    }
}

project(":android") {
    apply plugin: "com.android.application"
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.firebase-perf'
    apply plugin: 'com.google.firebase.crashlytics'

    configurations { natives }

    dependencies {
        implementation project(":core")
        implementation "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        implementation "com.badlogicgames.gdxpay:gdx-pay-android-googlebilling:$gdxPayVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"
        implementation 'androidx.annotation:annotation:1.9.1'

        def acraVersion = '5.7.0'
        implementation "ch.acra:acra-http:$acraVersion"
        implementation "ch.acra:acra-toast:$acraVersion"

        implementation "com.google.android.gms:play-services-games-v2:20.1.2"
        implementation 'com.github.bumptech.glide:glide:4.12.0'
        implementation 'com.google.android.gms:play-services-ads:24.2.0'

        implementation platform('com.google.firebase:firebase-bom:33.12.0')
        implementation 'com.google.firebase:firebase-analytics'
        implementation 'com.google.firebase:firebase-perf'
        implementation 'com.google.firebase:firebase-crashlytics'
    }
}

project(":core") {
    apply plugin: "java-library"
    apply plugin: "jacoco"

    dependencies {
        implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
        implementation "com.badlogicgames.gdxpay:gdx-pay-client:$gdxPayVersion"
        testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.3'
        testImplementation "org.mockito:mockito-core:5.3.1"
        testImplementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
        testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        testImplementation group: 'org.apache.poi', name: 'poi', version: '3.15'
        testImplementation group: 'org.apache.poi', name: 'poi-ooxml', version: '3.15'
    }
}

tasks.eclipse.doLast {
    delete ".project"
}

gradle.beforeProject { project ->
    if (gradle.startParameter.projectProperties.containsKey("isDebug")) {
        project.ext.set("isDebug", gradle.startParameter.projectProperties.get("isDebug"))
    }
}
